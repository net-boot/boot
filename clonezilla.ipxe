#!ipxe

goto ${menu} ||

:live_menu
set os Clonezilla
menu ${os} - Current Arch [ ${arch} ]
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
item --gap ${os} Versions
item debian ${space} ${os} Debian Based
item ubuntu ${space} ${os} Ubuntu Based
choose live_version || goto live_exit
menu ${os} ${live_version}
item --gap ${os} Flavors
goto ${live_version}

:debian
item /debian-squash/releases/download/2.6.6-11-f0e77646/ ${space} Clonezilla Debian Testing
item /debian-squash/releases/download/2.6.5-21-3f1c0d13/ ${space} Clonezilla Debian Stable
choose path || goto live_menu
goto clonezilla-boot

:ubuntu
item /ubuntu-squash/releases/download/20200302-eoan-355f8d57/ ${space} Clonezilla Ubuntu Stable
item /ubuntu-squash/releases/download/20200414-focal-fb737dc0/ ${space} Clonezilla Ubuntu Testing
choose path || goto live_menu
goto clonezilla-boot

:clonezilla-boot
imgfree
set url ${live_endpoint}${path}
kernel ${url}vmlinuz boot=live username=user union=overlay config components noswap edd=on nomodeset ocs_live_run="ocs-live-general" ocs_live_batch=no net.ifnames=0 nosplash noprompt fetch=${url}filesystem.squashfs initrd=initrd
initrd ${url}initrd
boot

:live_exit
clear menu
exit 0

